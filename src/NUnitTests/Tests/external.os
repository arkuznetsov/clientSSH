// ----------------------------------------------------------
// Use of this source code is governed by an MIT-style
// license that can be found in the LICENSE file or at
// https://opensource.org/licenses/MIT.
// ----------------------------------------------------------
// Codebase: https://github.com/ArKuznetsov/oscript-ssh/
// ----------------------------------------------------------

// #Использовать "build"

Перем юТест;
Перем СерверАдрес;
Перем СерверПорт;
Перем ПользовательИмя;
Перем ПользовательПАроль;
Перем ПутьККлючу;

////////////////////////////////////////////////////////////////////
// Программный интерфейс

Функция ПолучитьСписокТестов(ЮнитТестирование) Экспорт

	юТест = ЮнитТестирование;
	
	ВсеТесты = Новый Массив;

	ВсеТесты.Добавить("ТестДолжен_СоздатьЛибу");

	ВсеТесты.Добавить("ТестДолжен_ПолучитьПоток");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьСоединение");

	ВсеТесты.Добавить("ТестДолжен_Подключиться");
	ВсеТесты.Добавить("ТестДолжен_ПодключитьсяСКлючом");
	ВсеТесты.Добавить("ТестДолжен_ОтправитьФайлПоПаролю");
	ВсеТесты.Добавить("ТестДолжен_ОтправитьФайлПоКлючу");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьФайлПоПаролю");
	ВсеТесты.Добавить("ТестДолжен_ПолучитьФайлПоКлючу");

	ПередЗапускомТестов();

	Возврат ВсеТесты;

КонецФункции // ПолучитьСписокТестов()

Процедура ПередЗапускомТестов()

	СерверАдрес = "localhost";
	СерверПорт = 2222;
	ПользовательИмя = ПолучитьПеременнуюСреды("SSH_TEST_USER");
	ПользовательПароль = ПолучитьПеременнуюСреды("SSH_TEST_PWD");
	
	ПутьККлючу = ОбъединитьПути(ТекущийСценарий().Каталог, "tools", "openssh", "files");
	ПутьККлючу = ОбъединитьПути(ПутьККлючу, "sftp-key");
	
	ПутьККомпоненте = ОбъединитьПути(ТекущийСценарий().Каталог, "src", "oscript-component", "bin");
	ПутьККомпоненте = ОбъединитьПути(ПутьККомпоненте, "Debug", "net452", "oscript-component.dll");
	
	Попытка
		ПодключитьВнешнююКомпоненту(ПутьККомпоненте);
		Сообщить("Компонента подключена.");
	Исключение
		Сообщить("Компонента подключена ранее!");
	КонецПопытки;

КонецПроцедуры // ПередЗапускомТестов()

Процедура ТестДолжен_СоздатьЛибу() Экспорт

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	юТест.ПроверитьРавенство(ТипЗнч(Либа), Тип("КлиентSSH"), "Не создался экзепляр"); 

КонецПроцедуры // ТестДолжен_СоздатьЛибу()

Процедура ТестДолжен_ПолучитьПоток() Экспорт

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	юТест.ПроверитьРавенство(ТипЗнч(Либа), Тип("КлиентSSH"), "Не создался экзепляр"); 
	
	Поток = Либа.ПолучитьПоток();
	юТест.ПроверитьРавенство(ТипЗнч(Поток), Тип("ПотокSSH"), "Не удалось получить поток"); 

КонецПроцедуры // ТестДолжен_ПолучитьПоток()

Процедура ТестДолжен_ПолучитьСоединение() Экспорт

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	юТест.ПроверитьРавенство(ТипЗнч(Либа), Тип("КлиентSSH"), "Не создался экзепляр"); 
	
	Соединение = Либа.ПолучитьСоединение();
	юТест.ПроверитьРавенство(ТипЗнч(Соединение), Тип("СоединениеSSH"), "Не удалось получить соединение"); 

КонецПроцедуры // ТестДолжен_ПолучитьСоединение()

Процедура ТестДолжен_Подключиться() Экспорт

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	Соединение = Либа.ПолучитьСоединение();
	
	Результат = Соединение.ВыполнитьКоманду("echo Привет");   
	
	// А = Символы.ПС; // Падает
	Результат = СтрЗаменить(Результат, Символ(10), "");
	юТест.ПроверитьРавенство(Результат, "Привет", "Нет ответа комманды");
	Результат = Соединение.ВыполнитьКоманду("echo test");
	Результат = СтрЗаменить(Результат, Символ(10), "");
	юТест.ПроверитьРавенство(Результат, "test", "Нет ответа комманды");

	Соединение.Отключиться();

КонецПроцедуры // ТестДолжен_Подключиться()

Процедура ТестДолжен_ПодключитьсяСКлючом() Экспорт

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	Либа.УстановитьКлюч(ПутьККлючу, "");

	Соединение = Либа.ПолучитьСоединение();

	Результат = Соединение.ВыполнитьКоманду("echo Привет");   

	// А = Символы.ПС; // Падает
	Результат = СтрЗаменить(Результат, Символ(10), "");
	юТест.ПроверитьРавенство(Результат, "Привет", "Нет ответа комманды");
	Результат = Соединение.ВыполнитьКоманду("echo test");
	Результат = СтрЗаменить(Результат, Символ(10), "");
	юТест.ПроверитьРавенство(Результат, "test", "Нет ответа комманды");

	Соединение.Отключиться();

КонецПроцедуры // ТестДолжен_ПодключитьсяСКлючом()

Процедура ТестДолжен_ОтправитьФайлПоПаролю() Экспорт

	ИмяТестовогоФайла = "testFile1.txt";
	ПутьККаталогу = ОбъединитьПути(ТекущийСценарий().Каталог, "test", "testFolder1");
	ТестовыйФайл = НовыйТекстовыйФайл(ИмяТестовогоФайла, "Это тестовый файл!", ПутьККаталогу);

	ПутьККаталогуЛокальный = ОбъединитьПути(ТекущийСценарий().Каталог, "tools", "openssh", "upload");
	ФайлДляПроверки = Новый Файл(ОбъединитьПути(ПутьККаталогуЛокальный, ТестовыйФайл.Имя));

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	Scp = Либа.ПолучитьScp();

	юТест.ПроверитьРавенство(ТипЗнч(Scp), Тип("СоединениеSCP"), "Не получилось scp"); 
	Scp.ОтправитьФайл(ТестовыйФайл.ПолноеИмя, СтрШаблон("./upload/%1", ТестовыйФайл.Имя));

	Scp.Отключиться();

	юТест.ПроверитьИстину(ФайлДляПроверки.Существует(), "Не удалось отправить файл по паролю");

	УдалитьФайлы(ПутьККаталогуЛокальный, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу);

КонецПроцедуры // ТестДолжен_ОтправитьФайлПоПаролю()

Процедура ТестДолжен_ОтправитьФайлПоКлючу() Экспорт

	ИмяТестовогоФайла = "testFile1.txt";
	ПутьККаталогу = ОбъединитьПути(ТекущийСценарий().Каталог, "test", "testFolder1");
	ТестовыйФайл = НовыйТекстовыйФайл(ИмяТестовогоФайла, "Это тестовый файл!", ПутьККаталогу);

	ПутьККаталогуЛокальный = ОбъединитьПути(ТекущийСценарий().Каталог, "tools", "openssh", "upload");
	ФайлДляПроверки = Новый Файл(ОбъединитьПути(ПутьККаталогуЛокальный, ТестовыйФайл.Имя));

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, "");
	Либа.УстановитьКлюч(ПутьККлючу, "");
	Scp = Либа.ПолучитьScp();

	юТест.ПроверитьРавенство(ТипЗнч(Scp), Тип("СоединениеSCP"), "Не получилось scp"); 
	Scp.ОтправитьФайл(ТестовыйФайл.ПолноеИмя, СтрШаблон("./upload/%1", ТестовыйФайл.Имя));

	Scp.Отключиться();

	юТест.ПроверитьИстину(ФайлДляПроверки.Существует(), "Не удалось отправить файл по ключу");

	УдалитьФайлы(ПутьККаталогуЛокальный, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу);

КонецПроцедуры // ТестДолжен_ОтправитьФайлПоКлючу()

Процедура ТестДолжен_ПолучитьФайлПоПаролю() Экспорт

	ИмяТестовогоФайла = "testFile1.txt";
	КонтрольныйТекст = "Это тестовый файл!";
	ПутьККаталогуЛокальный = ОбъединитьПути(ТекущийСценарий().Каталог, "tools", "openssh", "upload");
	ТестовыйФайл = НовыйТекстовыйФайл(ИмяТестовогоФайла, КонтрольныйТекст, ПутьККаталогуЛокальный);

	ПутьККаталогу = ОбъединитьПути(ТекущийСценарий().Каталог, "test", "testFolder1");
	ФайлДляПроверки = НовыйТекстовыйФайл(ТестовыйФайл.Имя, "", ПутьККаталогу);

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, ПользовательПароль);
	Scp = Либа.ПолучитьScp();
	Scp.ПолучитьФайл(СтрШаблон("./upload/%1", ТестовыйФайл.Имя), ФайлДляПроверки.ПолноеИмя);
	Scp.Отключиться();

	ДокументДляПроверки = Новый ТекстовыйДокумент();
	ДокументДляПроверки.Прочитать(ФайлДляПроверки.ПолноеИмя);

	юТест.ПроверитьРавенство(СокрЛП(ДокументДляПроверки.ПолучитьТекст()),
	                         КонтрольныйТекст,
	                         "Не удалось получить файл по паролю");

	УдалитьФайлы(ПутьККаталогуЛокальный, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу);

КонецПроцедуры // ТестДолжен_ПолучитьФайлПоПаролю()

Процедура ТестДолжен_ПолучитьФайлПоКлючу() Экспорт

	ИмяТестовогоФайла = "testFile1.txt";
	КонтрольныйТекст = "Это тестовый файл!";
	ПутьККаталогуЛокальный = ОбъединитьПути(ТекущийСценарий().Каталог, "tools", "openssh", "upload");
	ТестовыйФайл = НовыйТекстовыйФайл(ИмяТестовогоФайла, КонтрольныйТекст, ПутьККаталогуЛокальный);

	ПутьККаталогу = ОбъединитьПути(ТекущийСценарий().Каталог, "test", "testFolder1");
	ФайлДляПроверки = НовыйТекстовыйФайл(ТестовыйФайл.Имя, "", ПутьККаталогу);

	Либа = Новый КлиентSSH(СерверАдрес, СерверПорт, ПользовательИмя, "");
	Либа.УстановитьКлюч(ПутьККлючу, "");
	Scp = Либа.ПолучитьScp();
	Scp.ПолучитьФайл(СтрШаблон("./upload/%1", ТестовыйФайл.Имя), ФайлДляПроверки.ПолноеИмя);
	Scp.Отключиться();

	ДокументДляПроверки = Новый ТекстовыйДокумент();
	ДокументДляПроверки.Прочитать(ФайлДляПроверки.ПолноеИмя);

	юТест.ПроверитьРавенство(СокрЛП(ДокументДляПроверки.ПолучитьТекст()),
	                         КонтрольныйТекст,
	                         "Не удалось получить файл по ключу");

	УдалитьФайлы(ПутьККаталогуЛокальный, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу, ПолучитьМаскуВсеФайлы());
	УдалитьФайлы(ПутьККаталогу);

КонецПроцедуры // ТестДолжен_ПолучитьФайлПоКлючу()

Функция НовыйТекстовыйФайл(ИмяФайла, Знач Содержимое = "", Знач ПутьККаталогу = "")

	Текст = Новый ТекстовыйДокумент();
	Если ЗначениеЗаполнено(Содержимое) Тогда
		Текст.УстановитьТекст(Содержимое);
	КонецЕсли;

	Если НЕ ЗначениеЗаполнено(ПутьККаталогу) Тогда
		ПутьККаталогу = ПолучитьИмяВременногоФайла();
	КонецЕсли;

	ПутьКФайлу = ОбъединитьПути(ПутьККаталогу, ИмяФайла);

	Каталог = Новый Файл(ПутьККаталогу);
	Если НЕ (Каталог.Существует() И Каталог.ЭтоКаталог()) Тогда
		СоздатьКаталог(ПутьККаталогу);
	КонецЕсли;

	Текст.Записать(ПутьКФайлу);

	Возврат Новый Файл(ПутьКФайлу);

КонецФункции // НовыйТекстовыйФайл()